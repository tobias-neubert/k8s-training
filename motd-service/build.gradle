plugins {
  id 'java'
  id 'idea'
  id 'org.springframework.boot' version '3.0.1'
  id 'com.google.cloud.tools.jib' version '3.3.1'
  id 'io.spring.dependency-management' version '1.1.0'
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

configurations {
  published
  compileOnly {
    extendsFrom annotationProcessor
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  testImplementation('org.assertj:assertj-core')
  testImplementation('org.junit.jupiter:junit-jupiter-api')
  testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
}


tasks.withType(Test) {
  useJUnitPlatform()
}

task prepareKotlinBuildScriptModel {
}

springBoot {
  buildInfo()
}

bootJar {
  enabled = false
}

jar {
  enabled = true
}

def baseImage = 'docker.n7lab.io/n7lab/eclipse-temurin:17.0.2_8-jre'
def toImage = "neubert/${project.name}:${project.version}"
jib {
  from {
    image = baseImage
    auth {
      username = artifactoryUsername
      password = artifactoryPassword
    }
  }
  to {
    image = toImage
    auth {
      username = artifactoryUsername
      password = artifactoryPassword
    }
  }
  container {
    mainClass = 'com.neubert.scaffold.motdservice.MotdServiceApplication'
    ports = ['8080']
    labels = [
      'org.label-schema.schema-version'           : '1.0',
      'org.label-schema.vendor'                   : 'tobi@s-neubert.net',
      'org.label-schema.license'                  : "Copyright Â© ${yearOfBuild} Tobias Neubert".toString(),
      'org.label-schema.build-date'               : timestampOfBuild,
      'org.label-schema.version'                  : "${project.version}".toString(),
//      'org.label-schema.vcs-url'                  : 'https://bitbucket.n7lab.io/scm/brdy/babelfish-sql-nordex.git',
//      'org.label-schema.vcs-ref'                  : "${gitVersioner.currentSha1}".toString(),
      'com.birdy7.babelfish-sql.images.base-image': baseImage,
      'maintainer'                                : 'tobi@s-neubert.net'
    ]
  }
}